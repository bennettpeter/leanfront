apply plugin: 'com.android.application'

task gitinfo {
//    println 'bash update_dependencies.sh'.execute().text
    ext.describe = 'git describe --dirty'.execute().text.trim()
    ext.describe2 = 'git -C ../leanfront-mobile describe --dirty'.execute().text.trim()
    def split = ext.describe.split("-")
    def split2 = ext.describe2.split("-")
    ext.version = Integer.parseInt(split[1])
    ext.version2 = Integer.parseInt(split2[1])
    if (split.size() > 3)
        ext.dirty = '-' + split[3]
    else
        ext.dirty = ''
    ext.version = (ext.version + ext.version2) * 2
    ext.versionName = 'lft-' + ext.version + '-' + split[2] + ext.dirty
    println "Version Name: " + ext.versionName + ", Version Code: " + ext.version
}

android {
    lintOptions{
        disable 'MissingTranslation'
    }
    compileSdk 35
    defaultConfig {
        applicationId "org.mythtv.leanfront"
        minSdkVersion 21
        //noinspection EditedTargetSdkVersion
        targetSdk 35
        compileSdk 35
        versionCode gitinfo.ext.version
        versionName gitinfo.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.debug
            debuggable false
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
        debug {
            minifyEnabled false
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
    }
    buildFeatures {
        buildConfig = true
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    ndkVersion '28.2.13676358'
    namespace 'org.mythtv.leanfront'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.leanback:leanback:1.2.0'
    implementation 'androidx.recommendation:recommendation:1.0.0'
    implementation 'androidx.leanback:leanback-preference:1.2.0'
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'androidx.exifinterface:exifinterface:1.4.1'
    implementation('androidx.tracing:tracing:1.3.0')
    implementation 'com.github.bumptech.glide:glide:5.0.5'

    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'

    implementation project(':media-lib-exoplayer')
    implementation project(':media-lib-ui-leanback')
    implementation project(':media-lib-ui')
    implementation project(':media-lib-decoder-ffmpeg')
    implementation 'com.google.firebase:firebase-crashlytics-buildtools:3.0.6'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.14.1'

    implementation "android.arch.lifecycle:extensions:1.1.1"
    // To fix ClassNotFoundException: Didn't find class "java.util.function.Supplier"
    // On old android versions.
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
}

apply plugin: 'idea'
idea {
    module {
        excludeDirs.add(file('app/src/main/jni/ffmpeg'))
    }
}
